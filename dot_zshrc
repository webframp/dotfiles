# Base16 Shell
# BASE16_SHELL="$HOME/.base16-manager/chriskempson/base16-shell/"
BASE16_SHELL="$HOME/.config/base16-shell/"
[ -n "$PS1" ] && \
    [ -s "$BASE16_SHELL/profile_helper.sh" ] && \
    eval "$("$BASE16_SHELL/profile_helper.sh")"

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block, everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

## Profiling
#zmodload zsh/zprof

#source /etc/zshrc

HISTFILE=~/.zsh_history
HISTSIZE=10000
SAVEHIST=10000
# http://zsh.sourceforge.net/Doc/Release/Options.html
# Protect existing files
setopt NO_CLOBBER
# don't nice background tasks
setopt NO_BG_NICE
setopt NO_HUP
setopt NO_BEEP
# allow functions to have local options
setopt LOCAL_OPTIONS
# allow functions to have local traps
setopt LOCAL_TRAPS
# add timestamps to history
setopt EXTENDED_HISTORY
setopt PROMPT_SUBST
setopt COMPLETE_IN_WORD
setopt CORRECT
# adds history
setopt APPEND_HISTORY
# adds history incrementally and share it across sessions
setopt INC_APPEND_HISTORY
setopt SHARE_HISTORY
# don't record dupes in history
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_REDUCE_BLANKS
setopt HIST_IGNORE_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_VERIFY
setopt HIST_EXPIRE_DUPS_FIRST
# directory navigation
setopt AUTO_CD
setopt AUTO_PUSHD

# lazy load any custom functions
lazyload_fpath=$HOME/.zsh/autoload
fpath=($lazyload_fpath $fpath)
if [[ -d "$lazyload_fpath" ]]; then
    for func in $lazyload_fpath/*; do
        autoload -Uz ${func:t}
    done
fi
unset lazyload_fpath

# completion https://github.com/getantibody/antibody/issues/242#issuecomment-404588896
# https://github.com/caarlos0/dotfiles/commit/16250fdb5ae2c8f762bb0bfb743bad18e7ae387d
autoload -Uz compinit
for dump in ~/.zcompdump(N.mh+24); do
  compinit
done
compinit -C

# menu select
unsetopt menu_complete

# completion performance improvements
# Force prefix matching, avoid partial globbing on path
zstyle ':completion:*' accept-exact '*(N)'
# enable completion cache
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.local/share/zsh/cache

# Ignore completion for non-existent commands
zstyle ':completion:*:functions' ignored-patterns '(_*|pre(cmd|exec))'
#zstyle ':completion:*:functions' ignored-patterns '_*'


# completion behavior adjustments
# Case insensitive, partial-word and substring competion
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|=*' 'l:|=* r:|=*'
# zstyle ':completion:*:*:*:*:*' menu select
# zstyle ':completion:*' special-dirs true
#
# # Colors in the completion list
# zstyle ':completion:*' list-colors ''
# zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;34=0=01'

zstyle :omz:plugins:ssh-agent identities id_rsa id_ed25519_appsgit id_ed25519_github

# Aliases
alias reload!='exec "$SHELL" -l'
# http://zsh.sourceforge.net/Doc/Release/Expansion.html#Parameter-Expansion
(( $+commands[exa] )) && {
    alias l='exa'
    alias la='exa -la'
    alias ll='exa -lag'
    alias lg='exa -bghHliS --git'
}
alias alacritty='/Applications/Alacritty.app/Contents/MacOS/alacritty'

# Git
alias gpu='git pull --prune --tags --all'
alias gco='git checkout'
alias gpo='git push origin HEAD'
alias gpa='git push origin --all'
alias gst='git status'
alias gcmh='git commit --amend -C HEAD'

# Docker
alias dockerhostshell='docker run -it --privileged --pid=host debian nsenter -t 1 -m -u -n -i sh'

# Emacs
alias doom='~/.emacs.d/bin/doom'
alias oc='~/.emacs.d/bin/org-capture'
if [[ "$INSIDE_EMACS" = 'vterm' ]]; then
    alias clear='vterm_printf "51;Evterm-clear-scrollback";tput clear'
fi

# Kubernetes
(( $+commands[kubectl] )) && {
    alias k='nocorrect kubectl'
    alias kshell='k run alpine-tmp-shell --rm -i --tty --image alpine:latest -- /bin/sh#'
    #compdef k=kubectl
    # kubectl get --raw /apis/custom.metrics.k8s.io/v1beta1
}

(( $+commands[kubectx] )) && {
    alias kx='kubectx'
    compdef kx=kubectx
}

(( $+commands[kubens] )) && {
    alias kn='kubens'
    compdef kn=kubens
}

# TODO members, groups, vault, Golang, Rust
cat() {bat $* --plain}

alias c=chezmoi

# Homebrew
alias brews='brew list -1'
alias yay='brew outdated && brew upgrade && brew cleanup'

# Ruby
alias be='bundle exec'

# Pass
alias qp='pass -c "$(find -L "$HOME/.password-store" \( -name .git\* -o -name .gpg-id \) -prune -o $@ -print 2>/dev/null | sed -e "s#${HOME}/.password-store/\{0,1\}##" -e 's#\.gpg##'|sort|fzf)"'
alias qpo='pass otp -c "$(find -L "$HOME/.password-store" \( -name .git\* -o -name .gpg-id \) -prune -o $@ -print 2>/dev/null | sed -e "s#${HOME}/.password-store/\{0,1\}##" -e 's#\.gpg##'|sort|fzf)"'

# NPM
(( $+commands[npm] )) && {
   alias ng="npm list -g --depth=0 2>/dev/null"
   alias nl="npm list --depth=0 2>/dev/null"
}

(( $+commands[direnv] )) && {
    _direnv_hook() {
        eval "$(direnv export zsh)";
    }
    typeset -ag precmd_functions;
    if [[ -z ${precmd_functions[(r)_direnv_hook]} ]]; then
        precmd_functions+=_direnv_hook;
    fi
}

autoload -U +X bashcompinit && bashcompinit
complete -o nospace -C /usr/local/bin/vault vault
complete -C /usr/local/bin/terraform terraform

# When from nix pkg
# https://github.com/NixOS/nixpkgs-channels/blob/nixos-unstable/pkgs/tools/admin/bash-my-aws/default.nix
#eval "$(bma-init)"
# When from git repo
if [ -d ${HOME}/.bash-my-aws ]; then
    source ~/.bash-my-aws/aliases
    source ~/.bash-my-aws/bash_completion.sh
fi

source $HOME/.zsh/correction.zsh
source $HOME/.zsh/dots.zsh

if test -f "$HOME/.zsh/local.zsh"; then
    source $HOME/.zsh/local.zsh
fi

# Load colors from less, and others.
[[ -f ~/.LESS_TERMCAP ]] && . ~/.LESS_TERMCAP

autoload -U select-word-style
select-word-style bash

## ZAW settings
zstyle ':filter-select' case-insensitive yes
zstyle ':filter-select' hist-find-no-dups yes
bindkey '^Xr' zaw-history
bindkey '^ ' autosuggest-accept

# vterm setup
vterm_printf(){
    if [ -n "$TMUX" ]; then
        # Tell tmux to pass the escape sequences through
        # (Source: http://permalink.gmane.org/gmane.comp.terminal-emulators.tmux.user/1324)
        printf "\ePtmux;\e\e]%s\007\e\\" "$1"
    elif [ "${TERM%%-*}" = "screen" ]; then
        # GNU screen (screen, screen-256color, screen-256color-bce)
        printf "\eP\e]%s\007\e\\" "$1"
    else
        printf "\e]%s\e\\" "$1"
    fi
}

if [[ "$INSIDE_EMACS" = 'vterm' ]]; then
    alias clear='vterm_printf "51;Evterm-clear-scrollback";tput clear'
fi

vterm_cmd() {
    local vterm_elisp
    vterm_elisp=""
    while [ $# -gt 0 ]; do
        vterm_elisp="$vterm_elisp""$(printf '"%s" ' "$(printf "%s" "$1" | sed -e 's|\\|\\\\|g' -e 's|"|\\"|g')")"
        shift
    done
    vterm_printf "51;E$vterm_elisp"
}

find_file() {
    vterm_cmd find-file "$(realpath "$@")"
}

## Source plugins last
# dynamic method
# source <(antibody init)
# antibody bundle <~/.zsh_plugins.txt

# static method, after updates run:
# antibody bundle <~/.zsh_plugins.txt > ~/.zsh_plugins.sh
source ~/.zsh_plugins.sh

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
